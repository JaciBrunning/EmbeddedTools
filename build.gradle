plugins {
    id "com.gradle.plugin-publish" version "0.9.9"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "idea"
    id 'com.gradle.build-scan' version '1.15'
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
    jcenter()
}

dependencies {
    compile 'com.jcraft:jsch:0.1.53'
    compile 'com.gradle:build-scan-plugin:1.15'

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile('cglib:cglib-nodep:2.2')
    testCompile('org.objenesis:objenesis:1.2')
    testCompile gradleTestKit()
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

group = "jaci.gradle"
archivesBaseName = "EmbeddedTools"
version = "2018.08.20" // YYYY.MM.DD(revision)

sourceCompatibility = 1.8
targetCompatibility = 1.8

pluginBundle {
    website = 'https://github.com/JacisNonsense/EmbeddedTools'
    vcsUrl = 'https://github.com/JacisNonsense/EmbeddedTools'
    description = 'Additions to the model-based DSL for deploying Java and Native projects to remote targets'
    tags = ['remote', 'target', 'deploy', 'java', 'native']

    plugins {
        embeddedTools {
            id = "${project.group}.${project.archivesBaseName}"
            displayName = 'EmbeddedTools'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact(jar) {
                artifactId 'EmbeddedTools'
                if (project.hasProperty('localPublish')) {
                    classifier null
                    groupId 'gradle.plugin.' + group
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        gradleRio {
            id = 'jaci.gradle.EmbeddedTools'
            implementationClass = 'jaci.gradle.EmbeddedTools'
        }
    }
}

wrapper {
    gradleVersion = '4.9'
    distributionType = Wrapper.DistributionType.ALL
}
